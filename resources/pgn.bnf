PGNDB = PGN*
PGN = HEADERS MOVELIST RESULT [WS]
HEADERS = HEADER*
HEADER = BRACKET TAG WS VALUE BRACKET [WS]
TAG = ALPHANUM
VALUE = #'".*"'

MOVELIST = MOVE*
MOVE = <#"\d+\."> HALFMOVE [HALFMOVE]
HALFMOVE = [WS] (PAWN-MOVE | PAWN-PROMOTION | PIECE-MOVE | PIECE-FILE-MOVE | PIECE-RANK-MOVE | PIECE-FILE-RANK-MOVE | KINGCASTLE | QUEENCASTLE) [DECORATION] [COMMENT] [WS]

<PIECE> = 'K' | 'Q' | 'R' | 'N' | 'B'
<FILE> = #"[a-h]"
<RANK> = #"[1-8]"
COMMENT = [WS] <'{'> CHARACTERS* <'}'> WS
KINGCASTLE = <'O-O' | '0-0'>
QUEENCASTLE = <'O-O-O' | '0-0-0'>

<BASIC-MOVE> = ['x'] FILE RANK (* e4 *)
<PAWN-MOVE> = [FILE] BASIC-MOVE (* b4 / axb4 *)
<PAWN-PROMOTION> = [FILE 'x'] BASIC-MOVE '=' PIECE (* e8=Q / dxe1=Q *)
<PIECE-MOVE> = PIECE BASIC-MOVE (* Nf3 / Nxf3 *)
<PIECE-FILE-MOVE> = PIECE FILE BASIC-MOVE (* Nfe4 / Nfxe4 *)
<PIECE-RANK-MOVE> = PIECE RANK BASIC-MOVE (* N3e4 / N3xe4 *)
<PIECE-FILE-RANK-MOVE> = PIECE FILE RANK BASIC-MOVE (* Re1c1 / Re1xc1 *)
<MATE> = '#'
<CHECK> = '+' | '++'
<ANNOTATION> = ['!'+] ['?'+]
<DECORATION> = MATE | CHECK | ANNOTATION

WHITEWINS = <'1-0'>
BLACKWINS = <'0-1'>
DRAW = <'1/2-1/2'>
UNKNOWN = <'*'>
RESULT = WHITEWINS | BLACKWINS | DRAW | UNKNOWN

<STRING> = QUOTE CHARACTERS* QUOTE
<CHARACTERS> = (ALPHANUM | #"\s+" | PUNCT)
<ALPHANUM> = #"\p{Alnum}+"
<WS> = <#"\s+">
<QUOTE> = <'\"'>
<BRACKET> = <'['> | <']'>
<PUNCT> = #"\p{Punct}"